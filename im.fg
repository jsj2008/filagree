port = 9998

####### net

# node.fg demo:
#
# 1) server listens
# 2) client connects
# 3) clients sends 'hi'
# 4) server responds with 'hi2'

server = [

    'connected' : function(self, id)
        sys.print('server: connected ' + id)
    end,

    'messaged' : function(self, id, msg)
        sys.print('server: message from ' + id + ': ' + msg)
        sys.send(id, msg + '2', self)
    end,

    'sent' : function(self, id)
        sys.print('server: sent to ' + id)
    end

]

client = [

    'connected' : function(self, id)
        sys.print('client: connected ' + id)
        sys.send(id, 'hi', self)
    end,

    'messaged' : function(self, id, msg)
        sys.print('client: message from ' + id + ': ' + msg)
        sys.disconnect(id)
    end,

    'sent' : function(self, id)
        sys.print('client: sent to ' + id)
    end
]


#server starts to listen
sys.listen(port, server)



####### ui

import 'ui'

ctx = ['w':240, 'h':320,
       'client' : client,
       'port' : port]

ctx.select = function(ctx)
    sys.print('select ' + sys.args())
end

ctx.settings = function(ctx)
    sys.ui(ctx,'vertical', [
            ['form', ['display name', 'username', 'password']],

            ['horizontal', [
                ['button', ['text':'save',   'logic':sys.form_saver('settings')]],
                ['button', ['text':'cancel', 'logic':ctx.main]],
                ['button', ['text':'logout', 'logic':ctx.main]],
            ]],
        ],
        ctx.w, ctx.h)
    sys.load_form('settings')
end

ctx.main = function(ctx)
    sys.ui(ctx,'vertical', [
                ['horizontal', [
                    ['button', ['text':'add',      'logic':ctx.add]],
                    ['button', ['text':'settings', 'logic':ctx.settings]],
                ]],
                ['table', ['list':[7, 8, 9, 'x', 'y', 'z'], 'logic':ctx.select]]
            ],
            ctx.w, ctx.h)
end

ctx.add = function(ctx)
    sys.ui(ctx,'vertical', [
            ['form', ['username']],
            ['horizontal', [
                ['button', ['text':'add',       'logic':ctx.main]],
                ['button', ['text':'nevermind', 'logic':ctx.main]],
            ]]],
            ctx.w, ctx.h)
end


ctx.signup = function(ctx)
    sys.connect('127.0.0.1', ctx.port, ctx.client)
    ctx.main()
end

sys.ui(ctx,
       'vertical', [

            ['form', ['username', 'password']],

            ['horizontal', [
                ['button', ['text':'signin', 'logic':ctx.main]],
                ['button', ['text':'signup', 'logic':ctx.signup]],
            ]]
        ],
        ctx.w, ctx.h)

sys.loop()
